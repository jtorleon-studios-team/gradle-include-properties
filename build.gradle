plugins {
  id 'java'
  id 'maven-publish'
  id 'java-gradle-plugin'
  alias libs.plugins.gradle.plugin.publish
  alias libs.plugins.cadixdev.licenser
}

group = 'io.github.jtorleonstudiosteam'
version = '1.0.0'

repositories {
  mavenCentral()
}

dependencies {
  implementation libs.jetbrains.annotations
  testImplementation libs.bundles.junit
  testRuntimeOnly libs.junit.jupiter.engine
  testImplementation libs.bundles.mockito
}

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(libs.versions.javaToolchainVersion.get())
  }
  sourceCompatibility = libs.versions.javaTargetCompatibility.get()
}

tasks.withType(JavaCompile).configureEach {
  it.options.encoding = 'UTF-8'
  it.options.release.set(libs.versions.javaTargetCompatibility.get().toInteger())
}

test {
  useJUnitPlatform()
}

gradlePlugin {
  website = 'https://github.com/jtorleon-studios-team/gradle-include-properties'
  vcsUrl = 'https://github.com/jtorleon-studios-team/gradle-include-properties.git'
  plugins {
    gradleIncludeProperties {
      id = "${group}.gradleincludeproperties"
      displayName = 'Plugin Gradle Include Properties'
      description = 'Gradle plugin to easily include multiple property files into your build.'
      implementationClass = 'com.github.tmslpm.gradle.include.properties.Main'
      tags = ['properties', 'include-properties']
    }
  }
}

